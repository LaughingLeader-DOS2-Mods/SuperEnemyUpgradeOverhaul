Version 1
SubGoalCombiner SGC_AND
INITSECTION
// [BEGIN_NO_OSITOOLS]
LeaderUpdater_Register_Mod("EnemyUpgradeOverhaul", "LaughingLeader", 2,0,5,2);
LeaderUpdater_Register_ActiveGoal("EnemyUpgradeOverhaul", "LaughingLeader", "LLENEMY_ZZZ_Updater");
LeaderLib_Register_ModMenu("EnemyUpgradeOverhaul.Settings", "[Enemy Upgrade Overhaul] Settings", "LLENEMY_Settings", "EnemyUpgradeOverhaul", "LaughingLeader");
// [END_NO_OSITOOLS]
GlobalSetFlag("LLENEMY_DuplicationUpgradesDisabled"); // Opt-in
GlobalSetFlag("LLENEMY_VoidwokenSourceSpawningEnabled");
LLENEMY_Upgrades_Internal_Register_Start();
KBSECTION
// [BEGIN_NO_OSITOOLS]
IF
GlobalFlagCleared("LeaderLib_ExtenderIsActive")
AND
NOT DB_LLENEMY_Updater_RegisteredNonExtender(_)
THEN
DB_LLENEMY_Updater_RegisteredNonExtender(1);
LeaderUpdater_Register_Mod("EnemyUpgradeOverhaul", "LaughingLeader", 2,0,5,2);
LeaderUpdater_Register_ActiveGoal("EnemyUpgradeOverhaul", "LaughingLeader", "LLENEMY_ZZZ_Updater");
LeaderLib_Register_ModMenu("EnemyUpgradeOverhaul.Settings", "[Enemy Upgrade Overhaul] Settings", "LLENEMY_Settings", "EnemyUpgradeOverhaul", "LaughingLeader");

IF
GameStarted(_Level, _)
AND
IsGameLevel(_Level, 1)
AND
LeaderUpdater_QRY_ModUpdateNeeded("EnemyUpgradeOverhaul", "LaughingLeader", 2,0,5,2)
THEN
LeaderUpdater_Register_Mod("EnemyUpgradeOverhaul", "LaughingLeader", 2,0,5,2);
// [END_NO_OSITOOLS]

PROC
LeaderUpdater_ModUpdated("EnemyUpgradeOverhaul", "LaughingLeader", (STRING)_OldVersion, (STRING)_NewVersion)
THEN
DB_NOOP(1);

PROC
LeaderLib_Mods_OnVersionChanged(046aafd8-ba66-4b37-adfb-519c1a5d04d7, (INTEGER)_LastVersion, (INTEGER)_NewVersion)
AND
_LastVersion < 536870923
THEN
GlobalSetFlag("LLENEMY_VoidwokenSourceSpawningEnabled");

PROC
LeaderLib_Mods_OnVersionChanged(046aafd8-ba66-4b37-adfb-519c1a5d04d7, (INTEGER)_LastVersion, (INTEGER)_NewVersion)
AND
_LastVersion < 536936449
THEN
SysClear("DB_LLENEMY_HighestLoremaster", 1); // Removed

//REGION DEBUG
IF
TextEventSet("llenemy_printall")
THEN
LLENEMY_Debug_PrintAllEntries();

PROC
LLENEMY_Debug_PrintAllEntries()
THEN
LeaderLog_Log("DEBUG", "[LLENEMY_00_UpgradesMain:] Printing all groups/types.");

PROC
LLENEMY_Debug_PrintAllEntries()
AND
DB_LLENEMY_Upgrades_TypeRollValues(_Group, _Type, _StartInt, _RollMaxInt)
AND
IntegertoString(_StartInt, _StartIntStr)
AND
IntegertoString(_RollMaxInt, _RollMaxIntStr)
THEN
LeaderLog_Log("DEBUG", "[DB_LLENEMY_Upgrades_TypeRollValues] Group(", _Group, ") Type(", _Type, ") AddStart(",_StartIntStr,") RollMax(",_RollMaxIntStr, ")");

PROC
LLENEMY_Debug_PrintAllEntries()
AND
DB_LLENEMY_Upgrades_TypeRollValues(_Group, _Type, _, _RollMaxInt)
AND
Real(_RollMaxInt, _RollMax)
AND
DB_LLENEMY_Upgrades_Statuses(_Group, _Type, _Status, _StartInt, _EndInt, _Duration, _ChallengePoints)
AND
IntegerSubtract(_StartInt, 1, _StartAdjusted)
AND
IntegerSum(_EndInt, 1, _EndAdjusted)
AND
Real(_StartAdjusted, _start)
AND
Real(_EndAdjusted, _end)
AND
RealSubtract(_end, _start, _b)
AND
RealDivide(_b, _RollMax, _d)
AND
RealProduct(_d, 100.0, _percentage)
AND
Integer(_percentage, _PercentageInt)
AND
IntegertoString(_PercentageInt, _PercentageIntStr)
AND
IntegertoString(_StartInt, _StartIntStr)
AND
IntegertoString(_EndInt, _EndIntStr)
THEN
LeaderLog_Log("DEBUG", "[DB_LLENEMY_Upgrades_Statuses] Group(", _Group, ") Type(", _Type, ") Status(",_Status,") ",_StartIntStr,"-",_EndIntStr, " (", _PercentageIntStr, "%)");
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader_EnemyUpgradeOverhaul"
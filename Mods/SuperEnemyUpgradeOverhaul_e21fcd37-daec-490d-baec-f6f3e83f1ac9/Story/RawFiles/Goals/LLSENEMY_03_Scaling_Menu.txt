Version 1
SubGoalCombiner SGC_AND
INITSECTION
LLSENEMY_Scaling_InitSettings();
KBSECTION
//REGION SETTINGS
PROC
LLSENEMY_Scaling_InitSettings()
THEN
DB_LLSENEMY_LevelModifier(0);
//END_REGION

//REGION MODIFIER_ADJUSTING
IF
ObjectFlagSet("LLENEMY_EnemyLeveling_Increase1", _Speaker, _)
THEN
ObjectClearFlag(_Speaker, "LLENEMY_EnemyLeveling_Increase1");
LLSENEMY_Scaling_AdjustLevelModifier(1);

IF
ObjectFlagSet("LLENEMY_EnemyLeveling_Decrease1", _Speaker, _)
THEN
ObjectClearFlag(_Speaker, "LLENEMY_EnemyLeveling_Decrease1");
LLSENEMY_Scaling_AdjustLevelModifier(-1);

IF
ObjectFlagSet("LLENEMY_EnemyLeveling_Reset", _Speaker, _)
THEN
ObjectClearFlag(_Speaker, "LLENEMY_EnemyLeveling_Reset");
LLSENEMY_Scaling_ResetLevelModifier();

IF
DialogStarted("LLENEMY_Menu_Scaling", _)
AND
GlobalGetFlag("LLENEMY_EnemyLevelingEnabled", _ScalingEnabled)
THEN
LLSENEMY_Scaling_UpdateDialogVariables(_ScalingEnabled);

IF
GlobalFlagSet("LLENEMY_EnemyLevelingEnabled")
THEN
LLSENEMY_Scaling_UpdateDialogVariables(1);

IF
GlobalFlagCleared("LLENEMY_EnemyLevelingEnabled")
THEN
LLSENEMY_Scaling_UpdateDialogVariables(0);

PROC
LLSENEMY_Scaling_UpdateDialogVariables()
AND
GlobalGetFlag("LLENEMY_EnemyLevelingEnabled", _ScalingEnabled)
THEN
LLSENEMY_Scaling_UpdateDialogVariables(_ScalingEnabled);

PROC
LLSENEMY_Scaling_UpdateDialogVariables((INTEGER)_ScalingEnabled)
AND
DB_LLSENEMY_LevelModifier(_Modifier)
THEN
DialogSetVariableInt("LLENEMY_Menu_Scaling", "LLENEMY_EnemyLeveling_Modifier_374d0f1d-bd61-4736-ad24-b1876a803c2f", _Modifier);

PROC
LLSENEMY_Scaling_UpdateDialogVariables(0)
THEN
DialogSetVariableString("LLENEMY_Menu_Scaling", "LLENEMY_EnemyLeveling_ExpectedLevel_4f2c6435-4ddc-4e2c-90d9-6506acb179ba", "Disabled");

PROC
LLSENEMY_Scaling_UpdateDialogVariables(1)
AND
CharacterGetHostCharacter(_Host)
AND
CharacterGetLevel(_Host, _Level)
AND
DB_LLSENEMY_LevelModifier(_Modifier)
AND
IntegerSum(_Level, _Modifier, _EnemyLevel)
AND
IntegertoString(_EnemyLevel, _EnemyLevelStr)
THEN
DialogSetVariableString("LLENEMY_Menu_Scaling", "LLENEMY_EnemyLeveling_ExpectedLevel_4f2c6435-4ddc-4e2c-90d9-6506acb179ba", _EnemyLevelStr);

PROC
LLSENEMY_Scaling_AdjustLevelModifier((INTEGER)_ByAmount)
AND
DB_LLSENEMY_LevelModifier(_Modifier)
AND
IntegerSum(_Modifier, _ByAmount, _NextModifier)
AND
IntegerMax(-1, _NextModifier, _ClampedModifier1)
AND
IntegerMin(_ClampedModifier1, 20, _ClampedModifier2)
THEN
NOT DB_LLSENEMY_LevelModifier(_Modifier);
DB_LLSENEMY_LevelModifier(_ClampedModifier2);
LLSENEMY_Scaling_UpdateDialogVariables();

PROC
LLSENEMY_Scaling_ResetLevelModifier()
AND
DB_LLSENEMY_LevelModifier(_Modifier)
THEN
NOT DB_LLSENEMY_LevelModifier(_Modifier);
DB_LLSENEMY_LevelModifier(0);
LLSENEMY_Scaling_UpdateDialogVariables();
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader_SuperEnemyUpgradeOverhaul"
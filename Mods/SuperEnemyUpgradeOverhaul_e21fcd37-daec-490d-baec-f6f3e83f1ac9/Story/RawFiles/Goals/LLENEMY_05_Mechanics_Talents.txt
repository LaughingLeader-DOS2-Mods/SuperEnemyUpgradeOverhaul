Version 1
SubGoalCombiner SGC_AND
INITSECTION
LLENEMY_Mechanics_Talents_InitSettings();

KBSECTION
PROC
LLENEMY_Mechanics_Talents_InitSettings()
THEN
// Stored 2 per entry for faster iterators
DB_LLENEMY_Talents_MasterThief_Tags("GRENADES");
DB_LLENEMY_Talents_MasterThief_Tags("LLWEAPONEX_Throwing");
DB_LLENEMY_Talents_MasterThief_Tags("Consumable");
DB_LLENEMY_Talents_MasterThief_Tags("Potion");

//REGION TOGGLE_SCRIPT_TOGGLING
IF
CharacterStatusRemoved(_Char, "LLENEMY_TALENT_RESISTDEAD", _)
THEN
LeaderLib_ToggleScripts_DisableScriptForObjectAfterDelay(_Char, "LLENEMY_ResistDead_Active", "EnemyUpgradeOverhaul", 3000);

IF
CharacterStatusRemoved(_Char, "LLENEMY_TALENT_RESISTDEAD2", _)
THEN
LeaderLib_ToggleScripts_DisableScriptForObjectAfterDelay(_Char, "LLENEMY_ResistDead_Active", "EnemyUpgradeOverhaul", 3000);

IF
CharacterStatusApplied(_Char, "LLENEMY_TALENT_RESISTDEAD", _)
THEN
LeaderLib_ToggleScripts_EnableScriptForObject(_Char, "LLENEMY_ResistDead_Active", "EnemyUpgradeOverhaul");

IF
CharacterStatusApplied(_Char, "LLENEMY_TALENT_RESISTDEAD2", _)
THEN
LeaderLib_ToggleScripts_EnableScriptForObject(_Char, "LLENEMY_ResistDead_Active", "EnemyUpgradeOverhaul");

IF
CharacterStatusRemoved(_Char, "LLENEMY_TALENT_BULLY", _)
THEN
LeaderLib_ToggleScripts_DisableScriptForObject(_Char, "LLENEMY_Bully_Active", "EnemyUpgradeOverhaul");

IF
CharacterStatusApplied(_Char, "LLENEMY_TALENT_BULLY", _)
THEN
LeaderLib_ToggleScripts_EnableScriptForObject(_Char, "LLENEMY_Bully_Active", "EnemyUpgradeOverhaul");

IF
CharacterStatusRemoved(_Char, "LLENEMY_TALENT_COUNTER", _)
THEN
LeaderLib_ToggleScripts_DisableScriptForObject(_Char, "LLENEMY_Counter_Active", "EnemyUpgradeOverhaul");

IF
CharacterStatusApplied(_Char, "LLENEMY_TALENT_COUNTER", _)
THEN
LeaderLib_ToggleScripts_EnableScriptForObject(_Char, "LLENEMY_Counter_Active", "EnemyUpgradeOverhaul");

IF
CharacterStatusRemoved(_Char, "LLENEMY_TALENT_MASTERTHIEF", _)
THEN
LeaderLib_ToggleScripts_DisableScriptForObject(_Char, "LLENEMY_MasterThief_Active", "EnemyUpgradeOverhaul");

IF
CharacterStatusApplied(_Char, "LLENEMY_TALENT_MASTERTHIEF", _)
THEN
LeaderLib_ToggleScripts_EnableScriptForObject(_Char, "LLENEMY_MasterThief_Active", "EnemyUpgradeOverhaul");

IF
CharacterStatusRemoved(_Char, "LLENEMY_TALENT_NATURALCONDUCTOR", _)
THEN
LeaderLib_ToggleScripts_DisableScriptForObject(_Char, "LLENEMY_NaturalConductor_Active", "EnemyUpgradeOverhaul");

IF
CharacterStatusApplied(_Char, "LLENEMY_TALENT_NATURALCONDUCTOR", _)
THEN
LeaderLib_ToggleScripts_EnableScriptForObject(_Char, "LLENEMY_NaturalConductor_Active", "EnemyUpgradeOverhaul");
//END_REGION

//REGION MASTER_THIEF_MUG
PROC
LLENEMY_Talents_MugTarget((CHARACTERGUID)_Enemy, (CHARACTERGUID)_Target)
AND
NOT DB_LLENEMY_Talents_Temp_MasterThief_Iterating(_Enemy, _Target, _)
THEN
DB_LLENEMY_Talents_Temp_MasterThief_Iterating(_Enemy, _Target, 0);
ApplyStatus(_Enemy, "LLENEMY_TALENT_MASTERTHIEF_COOLDOWN", 6.0, 0, _Enemy);
InventoryLaunchIterator(_Target, "Iterator_LLENEMY_MasterThief_FoundItem", "");
LeaderLib_Timers_StartCharacterCharacterTimer(_Enemy, _Target, 50, "Timers_LLENEMY_MugTarget_Finish", "LLENEMY_MugTarget_Finish");

IF
CharacterCharacterEvent(_Enemy, _Target, "LLENEMY_MugTarget_Finish")
THEN
LLENEMY_Talents_Internal_Mug_PickItem(_Enemy, _Target);
LLENEMY_Talents_Internal_Mug_ClearData(_Target);
// [BEGIN_NO_OSITOOLS]
LLENEMY_Talents_Internal_Mug_StealRandomGold(_Enemy, _Target);
// [END_NO_OSITOOLS]

QRY
LLENEMY_Talents_Mug_QRY_ItemHasTag((ITEMGUID)_Item)
AND
DB_LLENEMY_Talents_MasterThief_Tags(_Tag)
AND
IsTagged(_Item, _Tag, 1)
THEN
DB_NOOP(1);

IF
StoryEvent((ITEMGUID)_Item, "Iterator_LLENEMY_MasterThief_FoundItem")
AND
LLENEMY_Talents_Mug_QRY_ItemHasTag(_Item)
AND
GetInventoryOwner(_Item, (CHARACTERGUID)_Target)
AND
DB_LLENEMY_Talents_Temp_MasterThief_Iterating(_Enemy, _Target, _Index)
AND
IntegerSum(_Index, 1, _NextIndex)
THEN
NOT DB_LLENEMY_Talents_Temp_MasterThief_Iterating(_Enemy, _Target, _Index);
DB_LLENEMY_Talents_Temp_MasterThief_Iterating(_Enemy, _Target, _NextIndex);
DB_LLENEMY_Talents_Temp_MasterThief_Items(_Target, _Index, _Item);

// [BEGIN_NO_OSITOOLS]
PROC
LLENEMY_Talents_Internal_Mug_PickItem((CHARACTERGUID)_Enemy, (CHARACTERGUID)_Target)
AND
DB_LLENEMY_Talents_Temp_MasterThief_Iterating(_Enemy, _Target, _Count)
AND
IntegerSubtract(_Count, 1, _LastIndex)
AND
LeaderLib_Random_QRY(0, _LastIndex)
AND
DB_LeaderLib_Random(_Index)
AND
DB_LLENEMY_Talents_Temp_MasterThief_Items(_Target, _Index, _Item)
THEN
NOT DB_LeaderLib_Random(_Index);
ItemToInventory(_Item, _Enemy, 1, 0, 0);
LLENEMY_Talents_OnTargetMugged(_Enemy, _Target, _Item, 1);
// [END_NO_OSITOOLS]

/* [OSITOOLS_ONLY]
PROC
LLENEMY_Talents_Internal_Mug_PickItem((CHARACTERGUID)_Enemy, (CHARACTERGUID)_Target)
AND
GetUUID(_Target, _TargetUUID)
AND
GetUUID(_Enemy, _EnemyUUID)
THEN
NRD_ModCall("EnemyUpgradeOverhaul", "MugTarget_End", _EnemyUUID, _TargetUUID);
*/

PROC
LLENEMY_Talents_Internal_Mug_ClearData((CHARACTERGUID)_Target)
AND
DB_LLENEMY_Talents_Temp_MasterThief_Iterating(_Enemy, _Target, _Index)
THEN
NOT DB_LLENEMY_Talents_Temp_MasterThief_Iterating(_Enemy, _Target, _Index);

PROC
LLENEMY_Talents_Internal_Mug_ClearData((CHARACTERGUID)_Target)
AND
DB_LLENEMY_Talents_Temp_MasterThief_Items(_Target, _Index, _Item)
THEN
NOT DB_LLENEMY_Talents_Temp_MasterThief_Items(_Target, _Index, _Item);

PROC
LLENEMY_Talents_Internal_Mug_StealRandomGold((CHARACTERGUID)_Enemy, (CHARACTERGUID)_Target)
AND
CharacterGetGold(_Target, _Gold)
AND
_Gold > 0
AND
IntegerDivide(_Gold, 8, _GoldMax)
AND
IntegerMax(_GoldMax, 1, _CappedGold)
AND
IntegerProduct(_CappedGold, -1, _RemoveGold)
THEN
CharacterAddGold(_Target, _RemoveGold);
CharacterAddGold(_Enemy, _CappedGold);
CharacterDisplayTextWithParam(_Target, "LLENEMY_DisplayText_MuggedGold_Target", _CappedGold);
CharacterDisplayTextWithParam(_Enemy, "LLENEMY_DisplayText_MuggedGold_Enemy", _CappedGold);
//END_REGION

//REGION ON_MUGGED
PROC
LLENEMY_Talents_OnTargetMugged((CHARACTERGUID)_Enemy, (CHARACTERGUID)_Target, (ITEMGUID)_Item, (INTEGER)_StolenAmount)
THEN
DB_NOOP(1);
//END_REGION

EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader_EnemyUpgradeOverhaul"
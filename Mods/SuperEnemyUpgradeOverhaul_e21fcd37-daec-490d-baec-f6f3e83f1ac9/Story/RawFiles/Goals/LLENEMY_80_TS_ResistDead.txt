Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION
IF
CharacterPrecogDying(_Enemy)
AND
HasActiveStatus(_Enemy, "LLENEMY_TALENT_RESISTDEAD", 1)
THEN
DB_LLENEMY_Talents_Temp_ReviveNext(_Enemy, 20);

IF
CharacterPrecogDying(_Enemy)
AND
HasActiveStatus(_Enemy, "LLENEMY_TALENT_RESISTDEAD2", 1)
THEN
DB_LLENEMY_Talents_Temp_ReviveNext(_Enemy, 100);

IF
CharacterDied(_Enemy)
AND
DB_LLENEMY_Talents_Temp_ReviveNext(_Enemy, _Amount)
THEN
CharacterResurrect(_Enemy);

IF
CharacterStatusRemoved(_Enemy, "RESURRECT", _)
AND
DB_LLENEMY_Talents_Temp_ReviveNext(_Enemy, _Amount)
AND
Real(_Amount, _Percentage)
THEN
NOT DB_LLENEMY_Talents_Temp_ReviveNext(_Enemy, _Amount);
PlayEffect(_Enemy, "LLENEMY_FX_Status_ComebackKid_Resurrect_01");
CharacterSetHitpointsPercentage(_Enemy, _Percentage);
//Reroll buffs! Let the evil floowwwww.
LeaderLib_ToggleScripts_DisableScriptForObject(_Enemy, "LLENEMY_ResistDead_Active", "EnemyUpgradeOverhaul");
LLENEMY_Upgrades_ClearFlags(_Enemy);
LLENEMY_Upgrades_RollForUpgrades(_Enemy);

EXITSECTION
SysClear("DB_LLENEMY_Talents_Temp_ReviveNext", 2);
ENDEXITSECTION
ParentTargetEdge "LLENEMY_80_ToggledScripts"
Version 1
SubGoalCombiner SGC_AND
INITSECTION
KBSECTION
IF
NRD_OnHit((CHARACTERGUID)_Goblin, (CHARACTERGUID)_Source, _Damage, _Handle)
AND
NOT DB_LLSENEMY_TreasureGoblins_Temp_JustHit(_Goblin, _)
AND
_Damage > 0
AND
IsTagged(_Goblin, "LLENEMY_TreasureGoblin", 1)
AND
CharacterIsPlayer(_Source, 1)
AND
LeaderLib_Hit_QRY_HitSucceeded(_Goblin, _Handle, 0)
AND
LeaderLib_Hit_QRY_HitDirectly(_Goblin, _Handle, 0)
THEN
DB_LLSENEMY_TreasureGoblins_Temp_JustHit(_Goblin, _Source);
LeaderLib_Variables_ModifyVariableInt(_Goblin, "LLENEMY_TreasureGoblin_TotalHits", 1);
//LeaderLib_Timers_StartCharacterCharacterTimer(_Goblin, _Source, 550, "Timers_LLENEMY_TreasureGoblins_ClearJustHit", "LLENEMY_TreasureGoblins_ClearJustHit");
LeaderLib_Timers_StartObjectObjectTimer(_Goblin, _Source, 550, "Timers_LLENEMY_TreasureGoblins_ClearJustHit", "LLENEMY_TreasureGoblins_ClearJustHit");

PROC
LeaderLib_Timers_ObjectObjectTimerFinished(_Goblin, _Source, "LLENEMY_TreasureGoblins_ClearJustHit")
AND
DB_LLSENEMY_TreasureGoblins_Temp_JustHit((CHARACTERGUID)_Goblin, (CHARACTERGUID)_Source)
THEN
NOT DB_LLSENEMY_TreasureGoblins_Temp_JustHit(_Goblin, _Source);

IF
CharacterCharacterEvent(_Goblin, _Source, "LLENEMY_TreasureGoblins_ClearJustHit")
AND
DB_LLSENEMY_TreasureGoblins_Temp_JustHit(_Goblin, _Source)
THEN
NOT DB_LLSENEMY_TreasureGoblins_Temp_JustHit(_Goblin, _Source);

IF
OnStageChanged((CHARACTERGUID)_Goblin, 0)
AND
DB_LLSENEMY_TreasureGoblins_Temp_Active(_Goblin)
THEN
NOT DB_LLSENEMY_TreasureGoblins_Temp_Active(_Goblin);
//RemoveTemporaryCharacter(_Goblin);
LLSENEMY_Rewards_TreasureGoblin_ToggleScript(0);

/*
IF
ObjectEnteredCombat((CHARACTERGUID)_Goblin, _)
AND
DB_LLSENEMY_TreasureGoblins_Temp_Active(_Goblin)
THEN
JumpToTurn(_Goblin);
*/

IF
RegionEnded(_)
THEN
LeaderLib_ToggleScripts_DisableScript("LLENEMY_TreasureGoblin_Active", "SuperEnemyUpgradeOverhaul");

EXITSECTION
SysClear("DB_LLSENEMY_TreasureGoblins_Temp_Active", 1);
SysClear("DB_LLSENEMY_TreasureGoblins_Temp_JustHit", 2);
ENDEXITSECTION
ParentTargetEdge "LLSENEMY_80_ToggledScripts"
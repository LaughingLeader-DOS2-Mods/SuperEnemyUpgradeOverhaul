Version 1
SubGoalCombiner SGC_AND
INITSECTION
LLENEMY_System_InitSettings();
KBSECTION

PROC
LLENEMY_System_InitSettings()
THEN
LLENEMY_System_RegisterAlignments();
LLENEMY_System_RegisterTags();
LLENEMY_System_RegisterIgnoredCopyStatuses();
LLENEMY_System_RegisterIgnoredCharacters();

/* Alignments that are checked to see if a character is an "Enemy", for the purpose of adding upgrades in world commitment mode. */
PROC
LLENEMY_System_RegisterAlignments()
THEN
SysClear("DB_LLENEMY_EnemyAlignments", 1);
DB_LLENEMY_EnemyAlignments("Evil");
DB_LLENEMY_EnemyAlignments("Evil NPC");

/* Tags that are checked to see if a character is an "Enemy", for the purpose of adding upgrades in world commitment mode. */
PROC
LLENEMY_System_RegisterTags()
THEN
SysClear("DB_LLENEMY_EnemyTags", 1);
DB_LLENEMY_EnemyTags("MAGISTER");
DB_LLENEMY_EnemyTags("AGGRESSIVEANIMAL");
DB_LLENEMY_EnemyTags("VOIDWOKEN");
DB_LLENEMY_EnemyTags("MONSTROUS");
DB_LLENEMY_EnemyTags("BADASSCIVILIAN");
DB_LLENEMY_EnemyTags("AI_PREFERRED_TARGET");

/* Statuses that shouldn't be copied by Shadow Duplicants. */
PROC
LLENEMY_System_RegisterIgnoredCopyStatuses()
THEN
SysClear("DB_LLENEMY_IgnoredDuplicantStatuses", 1);
SysClear("DB_LLENEMY_IgnoredDuplicantStatusTypes", 1);
DB_LLENEMY_IgnoredDuplicantStatuses("NULL");
DB_LLENEMY_IgnoredDuplicantStatuses("LLENEMY_UPGRADE_INFO");
DB_LLENEMY_IgnoredDuplicantStatuses("LLENEMY_FAKE_DYING");
DB_LLENEMY_IgnoredDuplicantStatuses("LLENEMY_SKILL_MASS_SHACKLES");
DB_LLENEMY_IgnoredDuplicantStatuses("LLENEMY_TALENT_RESISTDEAD");
DB_LLENEMY_IgnoredDuplicantStatuses("LLENEMY_TALENT_RESISTDEAD2");
DB_LLENEMY_IgnoredDuplicantStatusTypes("BLIND");
DB_LLENEMY_IgnoredDuplicantStatusTypes("CHARMED");
DB_LLENEMY_IgnoredDuplicantStatusTypes("DAMAGE_ON_MOVE");
DB_LLENEMY_IgnoredDuplicantStatusTypes("INCAPACITATED");
DB_LLENEMY_IgnoredDuplicantStatusTypes("INVISIBLE");
DB_LLENEMY_IgnoredDuplicantStatusTypes("KNOCKED_DOWN");
DB_LLENEMY_IgnoredDuplicantStatusTypes("MUTED");

PROC
LLENEMY_System_RegisterIgnoredCharacters()
THEN
SysClear("DB_LLENEMY_IgnoredCharacters", 1);
DB_LLENEMY_IgnoredCharacters((CHARACTERGUID)CHARACTERGUID_S_FTJ_Florence_c272a37a-85ea-438f-9871-1551ef45d338);

PROC
LeaderUpdater_ModUpdated("EnemyUpgradeOverhaul", "LaughingLeader", (STRING)_OldVersion, (STRING)_NewVersion)
THEN
LLENEMY_System_InitSettings();

//REGION ACTIVE_MODS_CHANGED
IF
SavegameLoaded(_,_,_,_)
AND
DB_LLENEMY_Upgrades_Temp_ModUpgradesActive(_UUID, _GoalName, 1)
AND
NOT LLENEMY_Upgrades_QRY_ModIsActive(_UUID, _GoalName)
THEN
NOT DB_LLENEMY_Upgrades_Temp_ModUpgradesActive(_UUID, _GoalName, 1);
DB_LLENEMY_Upgrades_Temp_ModsChanged(1);

IF
SavegameLoaded(_,_,_,_)
AND
DB_LLENEMY_Upgrades_Temp_ModsChanged(1)
THEN
NOT DB_LLENEMY_Upgrades_Temp_ModsChanged(1);
LeaderLog_Log("DEBUG", "[LLENEMY_19_ModdedUpgrades_Start:SavegameLoaded] Re-registering upgrades since active mods have changed.");
LLENEMY_Upgrades_Internal_Register_Start();
//END_REGION

//REGION QUERIES
QRY
LLENEMY_Upgrades_QRY_CanAddUpgrades((CHARACTERGUID)_Char)
AND
NOT DB_LLENEMY_IgnoredCharacters(_Char)
AND
CharacterIsSummon(_Char, 0)
AND
IsTagged(_Char, "LLENEMY_UpgradesDisabled", 0)
AND
ObjectGetFlag(_Char, "LLENEMY_WorldUpgradesAdded", 0)
AND
ObjectGetFlag(_Char, "LLENEMY_UpgradesAdded", 0)
THEN
DB_NOOP(1);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader_EnemyUpgradeOverhaul"
Version 1
SubGoalCombiner SGC_AND
INITSECTION
LLSENEMY_Scaling_LevelAllInCombat();
KBSECTION

//New region leveling
PROC
LeaderLib_Initialized((STRING)_Region)
AND
DB_GlobalFlag("LLENEMY_Scaling_CommitToWorld_Enabled")
AND
IsGameLevel(_Region, 1)
THEN
NRD_ModCall("SuperEnemyUpgradeOverhaul", "LevelUpRegion", _Region);

IF
RegionEnded(_Region)
THEN
SysClear("DB_LLSENEMY_Scaling_Temp_WaitForCombat", 1);

//REGION NEUTRAL_CRIMES
IF
NRD_OnPrepareHit((CHARACTERGUID)_Char, (CHARACTERGUID)_Player, _Damage, _)
AND
_Damage > 0
AND
CharacterIsPlayer(_Player, 1)
AND
CharacterIsInCombat(_Char, 0)
AND
CharacterIsNeutral(_Char, _Player, 1)
AND
GetUUID(_Char, _UUID)
THEN
NRD_ModCall("SuperEnemyUpgradeOverhaul", "LevelUpCharacter", _UUID);
//END_REGION

//REGION TRANSFORMED_CHARACTER_BUG_WORKAROUND

/* There's a game bug that resets transformed characters back to level 1 when you load a save. */
PROC
LeaderLib_Initialized((STRING)_Region)
AND
DB_LLSENEMY_Scaling_Temp_TransformedCharacters(_Region, _Char, _Level)
AND
ObjectExists(_Char, _Exists)
THEN
LLSENEMY_Scaling_CheckTransformedCharacterLevel(_Region, _Char, _Level, _Exists);

//Stat resetting
IF
ObjectTransformed(_Char, _)
AND
ObjectIsCharacter((CHARACTERGUID)_Char, 1)
AND
NOT LeaderLib_Helper_QRY_CharacterIsPlayer(_Char)
AND
GetUUID(_Char, _UUID)
THEN
NRD_ModCall("SuperEnemyUpgradeOverhaul", "LevelUpCharacter", _UUID);
LLSENEMY_Scaling_SaveTransformedCharacterLevel(_Char);
//END_REGION

EXITSECTION
SysClear("DB_LLSENEMY_Scaling_Temp_WaitForCombat", 1);
ENDEXITSECTION
ParentTargetEdge "LLSENEMY_80_ToggledScripts"
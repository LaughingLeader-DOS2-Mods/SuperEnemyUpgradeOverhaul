Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION
//REGION SPAWNING
IF
CombatStarted(_CombatID)
AND
NOT DB_CurrentLevel("TUT_Tutorial_A")
AND
LeaderLib_Combat_QRY_HasInvolvedPlayer(_CombatID)
AND
LLENEMY_TreasureGoblins_QRY_CanRoll()
THEN
LLENEMY_TreasureGoblins_Internal_RollForSpawn(_CombatID);

QRY
LLENEMY_TreasureGoblins_QRY_CanRoll()
AND
DB_LLENEMY_TreasureGoblins_WaitCounter(_Last)
AND
IntegerSubtract(_Last, 1, _Next)
THEN
NOT DB_LLENEMY_TreasureGoblins_WaitCounter(_Last);
DB_LLENEMY_TreasureGoblins_WaitCounter(_Next);

QRY
LLENEMY_TreasureGoblins_QRY_CanRoll()
AND
DB_LLENEMY_TreasureGoblins_WaitCounter(0)
THEN
NOT DB_LLENEMY_TreasureGoblins_WaitCounter(0);

QRY
LLENEMY_TreasureGoblins_QRY_CanRoll()
AND
NOT DB_LLENEMY_TreasureGoblins_WaitCounter(_)
THEN
DB_NOOP(1);

PROC
LLENEMY_TreasureGoblins_Internal_RollForSpawn((INTEGER)_CombatID)
AND
NOT DB_LLENEMY_TreasureGoblins_Temp_Active(_)
AND
LeaderLib_Roll_QRY(10,999)
AND
LeaderLib_Random_QRY(4,12)
AND
DB_LeaderLib_Random(_CombatsUntilNextRoll)
THEN
NOT DB_LeaderLib_Random(_CombatsUntilNextRoll);
DB_LLENEMY_TreasureGoblins_WaitCounter(_CombatsUntilNextRoll);
LLENEMY_TreasureGoblins_Spawn(_CombatID);

PROC
LLENEMY_TreasureGoblins_Internal_RollForSpawn((INTEGER)_CombatID)
AND
NOT DB_LLENEMY_TreasureGoblins_Temp_Active(_)
THEN
LLENEMY_TreasureGoblins_Internal_IncreaseFailures(_CombatID);

PROC
LLENEMY_TreasureGoblins_Internal_IncreaseFailures((INTEGER)_CombatID)
AND
NOT DB_LLENEMY_Temp_TreasureGoblinRollFailures(_)
THEN
DB_LLENEMY_Temp_TreasureGoblinRollFailures(0);

PROC
LLENEMY_TreasureGoblins_Internal_IncreaseFailures((INTEGER)_CombatID)
AND
DB_LLENEMY_Temp_TreasureGoblinRollFailures(_Last)
AND
IntegerSum(_Last, 1, _Next)
THEN
NOT DB_LLENEMY_Temp_TreasureGoblinRollFailures(_Last);
DB_LLENEMY_Temp_TreasureGoblinRollFailures(_Next);
LeaderLog_LogInt("DEBUG", "[LLENEMY_08_TreasureGoblins:IncreaseFailures] Treasure Goblin roll attempts: (", _Last, " => ", _Next, ")");

PROC
LLENEMY_TreasureGoblins_Internal_IncreaseFailures((INTEGER)_CombatID)
AND
DB_LLENEMY_Temp_TreasureGoblinRollFailures(_TotalFailures)
AND
LeaderLib_Random_QRY(12,36)
AND
DB_LeaderLib_Random(_Failimit)
AND
_TotalFailures >= _Failimit
THEN
NOT DB_LLENEMY_Temp_TreasureGoblinRollFailures(_TotalFailures);
NOT DB_LeaderLib_Random(_Failimit);
LLENEMY_TreasureGoblins_Spawn(_CombatID);

PROC
LLENEMY_TreasureGoblins_Spawn((INTEGER)_CombatID)
AND
LeaderLib_Combat_QRY_GetRandomCharacter(_CombatID)
AND
DB_LeaderLib_Combat_Temp_RandomCharacter(_CombatID, _SpawnNear, _Index)
AND
GetPosition(_SpawnNear, _cx, _cy, _cz)
AND
FindValidPosition(_cx, _cy, _cz, 12.0, _SpawnNear, _x, _y, _z)
THEN
NOT DB_LeaderLib_Combat_Temp_RandomCharacter(_CombatID, _SpawnNear, _Index);
LLENEMY_TreasureGoblins_Internal_Spawn(_x, _y, _z);

PROC
LLENEMY_TreasureGoblins_Internal_Spawn((REAL)_x, (REAL)_y, (REAL)_z)
THEN
LLENEMY_TreasureGoblins_Internal_Spawn(_x, _y, _z, -1);

PROC
LLENEMY_TreasureGoblins_Internal_Spawn((REAL)_x, (REAL)_y, (REAL)_z, (INTEGER)_CombatID)
AND
CharacterGetHostCharacter(_Host)
AND
CharacterGetLevel(_Host, _Level)
//AND
//LeaderLib_Helper_QRY_GetHighestPlayerLevel()
//AND
//DB_LeaderLib_Helper_Temp_HighestPlayerLevel(_Level)
AND
CharacterCreateAtPosition(_x, _y, _z, "444e50a0-e59b-4866-b548-49a0197a0de1", 1, _Goblin)
THEN
NOT DB_LeaderLib_Helper_Temp_HighestPlayerLevel(_Level);
CharacterLevelUpTo(_Goblin, _Level);
LLENEMY_TreasureGoblins_Internal_OnGoblinSpawned(_Goblin, _CombatID, _x, _y, _z);
LLENEMY_Rewards_TreasureGoblin_ToggleScript(1);
DB_LLENEMY_TreasureGoblins_Temp_Active(_Goblin);
//LeaderLib_Helper_EnterCombatWithPlayers(_Goblin);
LeaderLib_Helper_MakeHostileToPlayers(_Goblin);
LeaderLib_Timers_StartObjectTimer(_Goblin, 1000, "Timers_LLENEMY_Goblin_EnterCombatWithPlayers", "LeaderLib_Commands_EnterCombatWithPlayers");

PROC
LLENEMY_TreasureGoblins_Internal_OnGoblinSpawned((CHARACTERGUID)_Goblin, (INTEGER)_CombatID, (REAL)_x, (REAL)_y, (REAL)_z)
AND
CharacterIsInCombat(_Goblin, 0)
AND
_CombatID > -1
AND
DB_CombatCharacters(_Player, _CombatID)
AND
DB_IsPlayer(_Player)
AND
CharacterIsInCombat(_Goblin, 0)
THEN
EnterCombat(_Goblin, _Player);
//END_REGION

//REGION TREASURE_DROPPING
PROC
LeaderLib_Variables_OnVariableIntChanged((GUIDSTRING)_Goblin, "LLENEMY_TreasureGoblin_TotalHits", (INTEGER)_LastValue, (INTEGER)_CurrentValue)
AND
_CurrentValue > _LastValue
THEN
LeaderLib_Timers_StartObjectTimer(_Goblin, 250, "Timers_LLENEMY_TreasureGoblins_SpawnLoot", "LLENEMY_TreasureGoblins_SpawnLoot");

IF
StoryEvent((CHARACTERGUID)_Goblin, "LLENEMY_TreasureGoblins_SpawnLoot")
AND
NOT GetVarInteger(_Goblin, "LLENEMY_TreasureGoblin_Defeated", 1)
AND
CharacterGetLevel(_Goblin, _Level)
AND
GetPosition(_Goblin, _x, _y, _z)
THEN
LLENEMY_TreasureGoblins_SpawnLoot(_Goblin, _x, _y, _z, _Level);

PROC
LLENEMY_TreasureGoblins_SpawnLoot((CHARACTERGUID)_Goblin, (REAL)_x, (REAL)_y, (REAL)_z, (INTEGER)_Level)
AND
GetUUID(_Goblin, _UUID)
THEN
LeaderLog_LogInt("DEBUG", "[LLENEMY_08_TreasureGoblins:SpawnLoot] Spawning loot at level ", _Level, "");
//GenerateTreasure(_Bag, "LLENEMY_TreasureGoblin_A", _Level, _Host);
CharacterGiveReward(_Goblin, "LLENEMY_TreasureGoblin_A", 1);
// [BEGIN_NO_OSITOOLS]
InventoryLaunchIterator(_Goblin, "LLENEMY_TreasureGoblins_TreasureFound", "");
// [END_NO_OSITOOLS]
/* [OSITOOLS_ONLY]
NRD_ModCall("EnemyUpgradeOverhaul", "TreasureGoblins_ScatterInventory", _UUID);
*/

IF
StoryEvent((ITEMGUID)_Item, "LLENEMY_TreasureGoblins_TreasureFound")
AND
GetInventoryOwner(_Item, (CHARACTERGUID)_Goblin)
AND
NOT LeaderLib_Helper_QRY_ItemIsEquipped(_Goblin, _Item)
AND
GetPosition(_Goblin, _x, _y, _z)
THEN
ItemScatterAt(_Item, _x, _y, _z);

IF
StoryEvent((ITEMGUID)_Item, "LLENEMY_TreasureGoblins_TreasureFound")
AND
GetTemplate(_Item, _Template)
AND
StringContains(_Template, "Gold", 0)
AND
ItemGetGoldValue(_Item, _Value)
AND
_Value >= 250
THEN
PlayEffect(_Item, "LLENEMY_FX_TreasureGoblin_Loot_Dropped_01");
//END_REGION

//REGION DEFEATED
// [BEGIN_NO_OSITOOLS]
IF
StoryEvent((CHARACTERGUID)_Goblin, "LLENEMY_TreasureGoblins_Defeated")
AND
NOT DB_LLENEMY_Rewards_Temp_DefeatedGoblin(_Goblin, _, _, _)
AND
GetVarInteger(_Goblin, "LLENEMY_TreasureGoblin_MaxTotalHits", _MaxTotalHits)
AND
GetVarInteger(_Goblin, "LLENEMY_TreasureGoblin_TotalHits", _TotalHits)
AND
IntegerSubtract(_MaxTotalHits, _MaxTotalHits, _RemainingHits)
AND
_RemainingHits > 0
AND
GetPosition(_Goblin, _x, _y, _z)
AND
CharacterGetLevel(_Goblin, _Level)
AND
CreateItemTemplateAtPosition("CONT_LLENEMY_Bag_TreasureGoblinSack_A_2b7888b9-833c-4443-b4b5-cc372b95b459", _x, _y, _z, _Bag)
THEN
DB_LLENEMY_Rewards_Temp_DefeatedGoblin(_Goblin, _Bag, _Level, _RemainingHits);
PlayEffect(_Bag,"RS3_FX_Skills_Void_Netherswap_Reappear_01");
LeaderLog_LogInt("DEBUG", "[LLENEMY_08_TreasureGoblins:Defeated] Goblin was defeated. Dumping loot for ", _RemainingHits, " hits.");
LeaderLib_Timers_StartObjectTimer(_Goblin, 25, "Timers_LLENEMY_TreasureGoblins_SpawnDefeatedLoot", "LLENEMY_TreasureGoblins_SpawnDefeatedLoot");

IF
StoryEvent((CHARACTERGUID)_Goblin, "LLENEMY_TreasureGoblins_SpawnDefeatedLoot")
AND
DB_LLENEMY_Rewards_Temp_DefeatedGoblin(_Goblin, _Bag, _Level, _TotalHits)
AND
_TotalHits > 0
AND
IntegerSubtract(_TotalHits, 1, _Next)
AND
GlobalGetFlag("LLENEMY_EnemyLevelingEnabled", _ScalingEnabled)
AND
LLENEMY_Rewards_QRY_GetTreasureLevel(_Goblin, _Level, _ScalingEnabled)
AND
DB_LLENEMY_Rewards_TreasureLevel(_Goblin, _TreasureLevel)
THEN
NOT DB_LLENEMY_Rewards_TreasureLevel(_Goblin, _TreasureLevel);
NOT DB_LLENEMY_Rewards_Temp_DefeatedGoblin(_Goblin, _Bag, _Level, _TotalHits);
DB_LLENEMY_Rewards_Temp_DefeatedGoblin(_Goblin, _Bag, _Level, _Next);
GenerateTreasure(_Bag, "LLENEMY_TreasureGoblin_A", _TreasureLevel, _Goblin);
LeaderLib_Timers_StartObjectTimer(_Goblin, 25, "Timers_LLENEMY_TreasureGoblins_SpawnDefeatedLoot", "LLENEMY_TreasureGoblins_SpawnDefeatedLoot");

IF
StoryEvent((CHARACTERGUID)_Goblin, "LLENEMY_TreasureGoblins_SpawnDefeatedLoot")
AND
DB_LLENEMY_Rewards_Temp_DefeatedGoblin(_Goblin, _Bag, _Level, _TotalHits)
AND
_TotalHits <= 0
THEN
NOT DB_LLENEMY_Rewards_Temp_DefeatedGoblin(_Goblin, _Bag, _Level, _TotalHits);
// [END_NO_OSITOOLS]

/* [OSITOOLS_ONLY]
IF
StoryEvent((CHARACTERGUID)_Goblin, "LLENEMY_TreasureGoblins_Defeated")
AND
GetUUID(_Goblin, _UUID)
THEN
NRD_ModCall("EnemyUpgradeOverhaul", "TreasureGoblinDefeated", _UUID);
*/

IF
StoryEvent((CHARACTERGUID)_Goblin, "LLENEMY_TreasureGoblins_Defeated")
THEN
LeaderLib_Timers_StartObjectTimer(_Goblin, 250, "Timers_LLENEMY_TreasureGoblins_Left", "LLENEMY_TreasureGoblins_Left");

IF
StoryEvent((CHARACTERGUID)_Goblin, "LLENEMY_TreasureGoblins_Left")
THEN
RemoveHarmfulStatuses(_Goblin);
Poof(_Goblin);
//END_REGION

//REGION TOGGLING_SCRIPT
PROC
LLENEMY_Rewards_TreasureGoblin_ToggleScript(1)
AND
NOT LeaderLib_ToggleScripts_QRY_ScriptIsEnabled("LLENEMY_TreasureGoblin_Active")
THEN
LeaderLib_ToggleScripts_EnableScript("LLENEMY_TreasureGoblin_Active", "EnemyUpgradeOverhaul");

PROC
LLENEMY_Rewards_TreasureGoblin_ToggleScript(0)
AND
NOT DB_LLENEMY_TreasureGoblins_Temp_Active(_)
THEN
LeaderLib_ToggleScripts_DisableScript("LLENEMY_TreasureGoblin_Active", "EnemyUpgradeOverhaul");
//END_REGION

//REGION ITEM_MOVING_TO_BAG
IF
StoryEvent((ITEMGUID)_Item, "LLENEMY_TreasureGoblins_AddItemToSack")
AND
DB_LLENEMY_TreasureGoblins_Temp_AddToSack((ITEMGUID)_Bag, _Item)
AND
ObjectExists(_Bag, 1)
AND
ItemGetAmount(_Item, _Amount)
THEN
NOT DB_LLENEMY_TreasureGoblins_Temp_AddToSack(_Bag, _Item);
ItemToInventory(_Item, _Bag, _Amount);

IF
ItemAddedToCharacter(_Item, _Char)
AND
DB_LLENEMY_TreasureGoblins_Temp_AddToSack(_Bag, _Item)
THEN
NOT DB_LLENEMY_TreasureGoblins_Temp_AddToSack(_Bag, _Item);

IF
ItemAddedToContainer(_Item, _Container)
AND
DB_LLENEMY_TreasureGoblins_Temp_AddToSack(_Bag, _Item)
THEN
NOT DB_LLENEMY_TreasureGoblins_Temp_AddToSack(_Bag, _Item);

IF
RegionEnded(_)
THEN
SysClear("DB_LLENEMY_TreasureGoblins_Temp_AddToSack", 2);
//END_REGION

//REGION INIT
PROC
LLENEMY_TreasureGoblins_Internal_OnGoblinSpawned((CHARACTERGUID)_Goblin, (INTEGER)_CombatID, (REAL)_x, (REAL)_y, (REAL)_z)
//StoryEvent((CHARACTERGUID)_Goblin, "LLENEMY_TreasureGoblins_OnInit")
AND
LeaderLib_Random_QRY(6,12)
AND
DB_LeaderLib_Random(_MaxHits)
THEN
NOT DB_LeaderLib_Random(_MaxHits);
SetVarFloat3(_Goblin, "LLENEMY_TreasureGoblin_StartPos", _x, _y, _z);
SetVarInteger(_Goblin, "LLENEMY_TreasureGoblin_PlayingAnim", 0);
SetVarInteger(_Goblin, "LLENEMY_TreasureGoblin_Defeated", 0);
SetVarInteger(_Goblin, "LLENEMY_TreasureGoblin_TotalHits", 0);
SetVarInteger(_Goblin, "LLENEMY_TreasureGoblin_MaxTotalHits", _MaxHits);

IF
StoryEvent((CHARACTERGUID)_Goblin, "LLENEMY_TreasureGoblins_OnTurnEnded")
AND
GetStatusTurns(_Goblin, "LLENEMY_TREASURE_GOBLIN", _TurnsLeft)
AND
_TurnsLeft >= 0
THEN
CharacterDisplayTextWithParam(_Goblin, "LLENEMY_DisplayText_TreasureGoblins_TurnsLeft", _TurnsLeft);
//END_REGION
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader_EnemyUpgradeOverhaul"

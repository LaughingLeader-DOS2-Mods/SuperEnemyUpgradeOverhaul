Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION
//REGION BONUS_UPGRADE_ROLLING
// [BEGIN_NO_OSITOOLS]
PROC
LLENEMY_Upgrades_OnRollingDone((CHARACTERGUID)_Char, (STRING)_Region)
AND
NOT DB_LLENEMY_HardMode_Temp_BonusRolls(_Char, _, _)
AND
LeaderLib_Random_QRY(1,3)
AND
DB_LeaderLib_Random(_Ran)
AND
IntegerSum(_Ran, 1, _BonusRolls)
THEN
NOT DB_LeaderLib_Random(_Ran);
DB_LLENEMY_HardMode_Temp_BonusRolls(_Char, _BonusRolls, 0);
LLENEMY_Upgrades_Internal_StartBonusTimer(_Char);

PROC
LLENEMY_Upgrades_Internal_StartBonusTimer((CHARACTERGUID)_Char)
AND
DB_LLENEMY_HardMode_Temp_BonusRolls(_Char, _BonusRolls, _Current)
AND
_Current < _BonusRolls
THEN
LeaderLib_Timers_StartObjectTimer(_Char, 250, "Timers_LLENEMY_Upgrades_RollBonusRolls", "LLENEMY_Upgrades_RollBonusRolls");

IF
StoryEvent((CHARACTERGUID)_Char, "LLENEMY_Upgrades_RollBonusRolls")
AND
DB_LLENEMY_HardMode_Temp_BonusRolls(_Char, _BonusRolls, _Current)
AND
_Current < _BonusRolls
AND
IntegerSum(_Current, 1, _Next)
THEN
NOT DB_LLENEMY_HardMode_Temp_BonusRolls(_Char, _BonusRolls, _Current);
DB_LLENEMY_HardMode_Temp_BonusRolls(_Char, _BonusRolls, _Next);
LLENEMY_Upgrades_RollForUpgrades(_Char);
LLENEMY_Upgrades_Internal_StartBonusTimer(_Char);
// [END_NO_OSITOOLS]

/* [OSITOOLS_ONLY]
PROC
LLENEMY_Upgrades_OnRollingDone((CHARACTERGUID)_Char, (STRING)_Region)
AND
GlobalGetFlag("LLENEMY_HardModeRollingDisabled", 0)
THEN
NRD_ModCall("EnemyUpgradeOverhaul", "HM_RollAdditionalUpgrades", (STRING)_Char);
*/

//Cleanup
IF
RegionEnded(_)
THEN
SysClear("DB_LLENEMY_HardMode_Temp_BonusRolls", 3);
//END_REGION
EXITSECTION
SysClear("DB_LLENEMY_HardMode_Temp_BonusRolls", 3);
ENDEXITSECTION
ParentTargetEdge "LLENEMY_80_ToggledScripts"

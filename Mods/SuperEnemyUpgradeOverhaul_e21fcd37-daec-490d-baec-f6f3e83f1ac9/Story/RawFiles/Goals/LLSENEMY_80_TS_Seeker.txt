Version 1
SubGoalCombiner SGC_AND
INITSECTION
KBSECTION

IF
ObjectEnteredCombat((CHARACTERGUID)_Char, _)
AND
HasActiveStatus(_Char, "LLENEMY_SEEKER", 1)
THEN
LeaderLib_Timers_StartObjectTimer(_Char, 1000, "Timers_LLENEMY_Statuses_Seeker_PulseNow", "LLENEMY_Statuses_Seeker_PulseNow");

IF
ObjectLeftCombat(_Char, _)
AND
LeaderLib_Helper_QRY_HasAnyStatus(_Char, "LLENEMY_SEEKER", "LLENEMY_SEEKER_DISABLED")
THEN
LeaderLib_Timers_CancelObjectTimer(_Char, "Timers_LLENEMY_Statuses_Seeker_PulseNow");

IF
ObjectWasTagged((CHARACTERGUID)_Char, "LeaderLib_IsDisabled")
AND
LeaderLib_Helper_QRY_HasStatus(_Char, "LLENEMY_SEEKER")
THEN
ApplyStatus(_Char, "LLENEMY_SEEKER_DISABLED", -1.0, 1, _Char);

IF
ObjectLostTag((CHARACTERGUID)_Char, "LeaderLib_IsDisabled")
AND
LeaderLib_Helper_QRY_HasStatus(_Char, "LLENEMY_SEEKER_DISABLED")
THEN
ApplyStatus(_Char, "LLENEMY_SEEKER", -1.0, 1, _Char);

IF
ObjectTurnStarted((CHARACTERGUID)_Char)
AND
LeaderLib_Helper_QRY_HasStatus(_Char, "LLENEMY_SEEKER")
THEN
DB_LLSENEMY_StatusLogic_Temp_WaitForSeekerAction(_Char);

IF
CharacterUsedSkill(_Char,_,_,_)
AND
DB_LLSENEMY_StatusLogic_Temp_WaitForSeekerAction(_Char)
THEN
NOT DB_LLSENEMY_StatusLogic_Temp_WaitForSeekerAction(_Char);

IF
CharacterStartAttackObject(_,_, _Char)
AND
DB_LLSENEMY_StatusLogic_Temp_WaitForSeekerAction(_Char)
THEN
NOT DB_LLSENEMY_StatusLogic_Temp_WaitForSeekerAction(_Char);

IF
CharacterUsedItem(_Char, _)
AND
DB_LLSENEMY_StatusLogic_Temp_WaitForSeekerAction(_Char)
THEN
NOT DB_LLSENEMY_StatusLogic_Temp_WaitForSeekerAction(_Char);

IF
CharacterDied(_Char)
AND
DB_LLSENEMY_StatusLogic_Temp_WaitForSeekerAction(_Char)
THEN
NOT DB_LLSENEMY_StatusLogic_Temp_WaitForSeekerAction(_Char);

IF
ObjectTurnEnded((CHARACTERGUID)_Char)
AND
DB_LLSENEMY_StatusLogic_Temp_WaitForSeekerAction(_Char)
THEN
NOT DB_LLSENEMY_StatusLogic_Temp_WaitForSeekerAction(_Char);
LLSENEMY_Mechanics_Statuses_Internal_SeekerTakeAction(_Char);

EXITSECTION
SysClear("DB_LLSENEMY_StatusLogic_Temp_WaitForSeekerAction", 1);
ENDEXITSECTION
ParentTargetEdge "LLSENEMY_80_ToggledScripts"
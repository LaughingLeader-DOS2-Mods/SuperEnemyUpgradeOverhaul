Version 1
SubGoalCombiner SGC_AND
INITSECTION
LLSENEMY_Hardmode_CheckForCampaign();
KBSECTION

//REGION CALLBACKS
PROC
LLSENEMY_Hardmode_Enabled()
THEN
NRD_ModCall("SuperEnemyUpgradeOverhaul", "Hardmode_Enabled");

PROC
LLSENEMY_Hardmode_Disabled()
THEN
NRD_ModCall("SuperEnemyUpgradeOverhaul", "Hardmode_Disabled");
//END_REGION

//REGION GLOBAL_FLAG_EVENT
IF
GlobalFlagSet("LLENEMY_HardmodeEnabled")
THEN
DB_LLSENEMY_Hardmode_ToggleTimer(1);
TimerCancel("Timers_LLENEMY_HardmodeToggled");
TimerLaunch("Timers_LLENEMY_HardmodeToggled", 3000);

IF
GlobalFlagCleared("LLENEMY_HardmodeEnabled")
THEN
DB_LLSENEMY_Hardmode_ToggleTimer(1);
TimerCancel("Timers_LLENEMY_HardmodeToggled");
TimerLaunch("Timers_LLENEMY_HardmodeToggled", 3000);

IF
DialogEnded("LLENEMY_Settings", _Instance)
AND
DB_LLSENEMY_Hardmode_ToggleTimer(1)
THEN
TimerCancel("Timers_LLENEMY_HardmodeToggled");
TimerLaunch("Timers_LLENEMY_HardmodeToggled", 5);

IF
TimerFinished("Timers_LLENEMY_HardmodeToggled")
THEN
NOT DB_LLSENEMY_Hardmode_ToggleTimer(1);

IF
TimerFinished("Timers_LLENEMY_HardmodeToggled")
AND
GlobalGetFlag("LLENEMY_HardmodeEnabled", 1)
THEN
LLSENEMY_Hardmode_Enabled();

IF
TimerFinished("Timers_LLENEMY_HardmodeToggled")
AND
GlobalGetFlag("LLENEMY_HardmodeEnabled", 0)
THEN
LLSENEMY_Hardmode_Disabled();
//END_REGION

//REGION ORIGINS_ENABLING
PROC
LLSENEMY_Hardmode_CheckForCampaign()
AND
GlobalGetFlag("LLENEMY_HardmodeEnabled", 1)
THEN
LLSENEMY_Hardmode_Enabled();

PROC
Proc_GameModeStarted("Campaign", (INTEGER)_IsEditorMode)
AND
GlobalGetFlag("LLENEMY_HardmodeEnabled", 1)
THEN
SysActivateGoal("LLSENEMY_30_Origins_Hardmode");

PROC
LLSENEMY_Hardmode_Enabled()
AND
SysStatus("DOS2ModWrapper", _Status)
THEN
SysActivateGoal("LLSENEMY_30_Origins_Hardmode");

IF
GameStarted(_,_)
THEN
TimerCancel("Timers_LLENEMY_HardmodeToggled");
TimerLaunch("Timers_LLENEMY_HardmodeToggled", 250);
//END_REGION

//REGION DISABLING
PROC
LLSENEMY_Hardmode_Disabled()
AND
SysIsActive("LLSENEMY_30_Origins_Hardmode")
THEN
SysCompleteGoal("LLSENEMY_30_Origins_Hardmode");

PROC
LLSENEMY_Hardmode_DisableScripts()
AND
DB_LLSENEMY_Hardmode_Origins_Scripts(_Region, _Script)
AND
SysIsActive(_Script)
THEN
SysCompleteGoal(_Script);
//END_REGION

//REGION NPC_SETUP
PROC
LLSENEMY_Hardmode_SetupNPC((CHARACTERGUID)_Char, (STRING)_Region, (INTEGER)_Immortal)
THEN
//LeaderLib_Helper_MakeHostileToPlayers(_Char);
SetFaction(_Char, "Evil NPC");
SetVarInteger(_Char, "FleeFromDangerousSurface", 0);

PROC
LLSENEMY_Hardmode_SetupNPC((CHARACTERGUID)_Char, (STRING)_Region, 1)
AND
CharacterIsInCombat(_Char, 0)
THEN
ProcSetInvulnerable(_Char, 1);
CharacterSetImmortal(_Char, 1);
//END_REGION

PROC
LLSENEMY_Hardmode_InitLevel((STRING)_Region)
AND
NOT DB_LLSENEMY_HM_InitializedLevel(_Region)
THEN
DB_LLSENEMY_HM_InitializedLevel(_Region);
NRD_ModCall("SuperEnemyUpgradeOverhaul", "Hardmode_InitLevel", _Region);

EXITSECTION
ENDEXITSECTION
ParentTargetEdge "LaughingLeader_SuperEnemyUpgradeOverhaul"
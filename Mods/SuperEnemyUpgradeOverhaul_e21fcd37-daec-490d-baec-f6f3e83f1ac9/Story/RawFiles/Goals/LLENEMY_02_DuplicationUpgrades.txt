Version 1
SubGoalCombiner SGC_AND
INITSECTION
KBSECTION
//REGION SETTINGS
PROC
LLENEMY_Upgrades_RegisterAll(0)
THEN
LLENEMY_Upgrades_AddGroup("Duplication", "LLENEMY_DuplicationUpgradesDisabled", "LLENEMY_DuplicationRolled", "LLENEMY_DuplicationUpgradeAdded");
LLENEMY_Upgrades_AddType("Duplication", "Elite", 1, 30, 8);
LLENEMY_Upgrades_AddType("Duplication", "Normal", 750, 800, 5);
LLENEMY_Upgrades_AddType("Duplication", "Weak", 950, 999, 3);

// Hard Mode
PROC
LLENEMY_Upgrades_RegisterAll(1)
THEN
LLENEMY_Upgrades_AddGroup("Duplication", "LLENEMY_DuplicationUpgradesDisabled", "LLENEMY_DuplicationRolled", "LLENEMY_DuplicationUpgradeAdded");
LLENEMY_Upgrades_AddType("Duplication", "Elite", 1, 120, 8);
LLENEMY_Upgrades_AddType("Duplication", "Normal", 400, 520, 5);
LLENEMY_Upgrades_AddType("Duplication", "Weak", 750, 850, 3);

PROC
LLENEMY_Upgrades_Register_TypeUpgrades("Duplication", "Elite", 0)
THEN
LLENEMY_Upgrades_AddStatus("Duplication", "Elite", "LLENEMY_DUPE_ELITE_DOUBLE", 10, 14);
LLENEMY_Upgrades_AddStatus("Duplication", "Elite", "LLENEMY_DUPE_ELITE_SINGLE", 989, 8);

PROC
LLENEMY_Upgrades_Register_TypeUpgrades("Duplication", "Elite", 1)
THEN
LLENEMY_Upgrades_AddStatus("Duplication", "Elite", "LLENEMY_DUPE_ELITE_DOUBLE", 20, 14);
LLENEMY_Upgrades_AddStatus("Duplication", "Elite", "LLENEMY_DUPE_ELITE_SINGLE", 978, 8);

PROC
LLENEMY_Upgrades_Register_TypeUpgrades("Duplication", "Normal", (INTEGER)_HardModeEnabled)
THEN
LLENEMY_Upgrades_AddStatus("Duplication", "Normal", "LLENEMY_DUPE_NORMAL_SAME", 999, 6);

PROC
LLENEMY_Upgrades_Register_TypeUpgrades("Duplication", "Weak", (INTEGER)_HardModeEnabled)
THEN
LLENEMY_Upgrades_AddStatus("Duplication", "Normal", "LLENEMY_DUPE_NORMAL_WEAKEST", 399, 2);
LLENEMY_Upgrades_AddStatus("Duplication", "Normal", "LLENEMY_DUPE_NORMAL_WEAK", 599, 4);
//END_REGION

//REGION SAVE_FIX
// Transformed characters revert to level 1 for some reason, upon loading a save
/*
IF
GameStarted(_Region,_)
AND
DB_LLENEMY_Duplication_Temp_Active(_Source, _Dupe, _Region)
AND
ObjectExists(_Dupe, 1)
AND
CharacterGetLevel(_Source, _Level)
AND
GetUUID(_Dupe, _UUID)
THEN
CharacterLevelUpTo(_Dupe, _Level);
NRD_ModCall("EnemyUpgradeOverhaul", "ClearGain", _UUID);

IF
StoryEvent((CHARACTERGUID)_Dupe, "LLENEMY_ApplyStats_Done")
AND
GetUUID(_Dupe, _UUID)
THEN
NRD_ModCall("EnemyUpgradeOverhaul", "ClearGain", _UUID);
*/
//END_REGION

//REGION UPDATES
/*
PROC
LeaderLib_Mods_OnVersionChanged(046aafd8-ba66-4b37-adfb-519c1a5d04d7, (INTEGER)_LastVersion, (INTEGER)_NextVersion)
AND
_LastVersion > -1
AND
_LastVersion < 536870929
AND
DB_LLENEMY_Duplication_Temp_Active(_Source, _Dupe, _Region)
THEN
NOT DB_LLENEMY_Duplication_Temp_Active(_Source, _Dupe, _Region);
LLENEMY_Duplication_Internal_SetupDupe(_Source, _Dupe, 0);
*/
//END_REGION

//REGION BLOCKING
// Ignore specific characters
PROC
LLENEMY_Upgrades_CanApplyUpgrade((CHARACTERGUID)_Enemy, "Duplication", (STRING)_Type, (STRING)_Status, (REAL)_Duration)
AND
NOT DB_LLENEMY_Origins_DuplicationBlocked(_Enemy)
AND
DB_LLENEMY_Duplication_Blacklist(_Enemy)
THEN
DB_LLENEMY_Origins_DuplicationBlocked(_Enemy);
LLENEMY_Upgrades_RedirectUpgradeToGroupType(_Enemy, "Duplication", _Type, _Status, "Buffs", "Normal");

PROC
LLENEMY_Upgrades_CanApplyGroupTypeUpgrades((CHARACTERGUID)_Enemy, "Duplication", (STRING)_Type)
AND
IsBoss(_Enemy, _IsBoss)
AND
NOT LLENEMY_Duplication_QRY_CanSpawnDuplicant(_Enemy, _IsBoss)
THEN
DB_LLENEMY_StatusUpgradeBlocked(_Enemy, "Duplication", _Type);
//END_REGION

//REGION CREATE
QRY
LLENEMY_Duplication_QRY_CanSpawnDuplicant((CHARACTERGUID)_Char, 1)
AND
GlobalGetFlag("LLENEMY_DuplicationUpgrades_BossesDisabled", 0)
AND
ObjectGetFlag(_Char, "LLENEMY_DuplicationUpgradeAdded", 0)
AND
IsTagged(_Char, "LLENEMY_DuplicationBlocked", 0)
THEN
DB_NOOP(1);

QRY
LLENEMY_Duplication_QRY_CanSpawnDuplicant((CHARACTERGUID)_Char, 0)
AND
IsTagged(_Char, "LLENEMY_DuplicationBlocked", 0)
THEN
DB_NOOP(1);

PROC
LLENEMY_Duplication_CreateDuplicant((CHARACTERGUID)_Source, (STRING)_Stats)
THEN
LLENEMY_Duplication_CreateDuplicant(_Source, _Stats, 0, 0, 0);

PROC
LLENEMY_Duplication_CreateDuplicant((CHARACTERGUID)_Source, (STRING)_Stats, (INTEGER)_CopyAttributes, (INTEGER)_CopyAbilities, (INTEGER)_CP)
AND
GetPosition(_Source, _x, _y, _z)
//AND GetTemplate(_Source, _Template)
AND
//"LLENEMY_Dupe_A_54ad4e06-b57f-46d0-90fc-5da1208250e0"
//CharacterCreateOutOfSightToObject(_Template, _Source, _Source, 0, "", _Dupe)
CharacterCreateAtPosition(_x, _y, _z, "LLENEMY_Dupe_A_54ad4e06-b57f-46d0-90fc-5da1208250e0", 0, _Dupe)
//CharacterCreateAtPosition(_x, _y, _z, _Template, 0, _Dupe)
THEN
SetVarInteger(_Dupe, "LLENEMY_ChallengePoints", _CP);
// [BEGIN_NO_OSITOOLS]
SetVarFixedString(_Dupe, "LLENEMY_Dupe_Stats", _Stats);
DB_LLENEMY_Duplication_Temp_CopySettings(_Source, _Dupe, _CopyAttributes, _CopyAbilities);
// [END_NO_OSITOOLS]
LLENEMY_Duplication_Internal_SetupDupe(_Source, _Dupe, 1);

/* [OSITOOLS_ONLY]
PROC
LLENEMY_Duplication_Internal_SetupDupe((CHARACTERGUID)_Source, (CHARACTERGUID)_Dupe, (INTEGER)_CopyStatuses)
AND
GetRegion(_Source, _Region)
AND
GetFaction(_Source, _Faction)
AND
GetUUID(_Source, _SourceUUID)
AND
GetUUID(_Dupe, _DupeUUID)
THEN
DB_LLENEMY_Duplication_Temp_Active(_Source, _Dupe, _Region);
SetOnStage(_Dupe, 0);
CharacterSetDetached(_Dupe, 1);
SetVarObject(_Dupe, "LLENEMY_Duplicant_Owner", _Source);
ApplyStatus(_Dupe, "LLENEMY_DUPLICANT", -1.0, 0, _Source);
//CharacterTransformFromCharacter(_Dupe, _Source, 1, 1, 1, 1, 1, 1, 0);
CharacterTransformAppearanceTo(_Dupe, _Source, 1, 1);
SetFaction(_Dupe, _Faction);
//ChangeAttitude(_Source, _Dupe, 200);
//ChangeAttitude(_Dupe, _Source, 200);
NRD_ModCall("EnemyUpgradeOverhaul", "Duplication_CopySource", _SourceUUID, _DupeUUID);
LeaderLib_Timers_StartObjectTimer(_Source, 750, "Timers_LLENEMY_Duplication_SetCPTag", "LLENEMY_Duplication_SetCPTag");

PROC
LLENEMY_Duplication_Internal_SetupDupe((CHARACTERGUID)_Source, (CHARACTERGUID)_Dupe, 1)
THEN
LeaderLib_Tags_PreserveTag(_Dupe, "LLENEMY_UpgradesDisabled");
//LeaderLib_Timers_StartObjectTimer(_Source, 250, "Timers_LLENEMY_Duplication_CopyStatuses", "LLENEMY_Duplication_CopyStatuses");

IF
StoryEvent(_Dupe, "LLENEMY_Duplication_SetCPTag")
AND
GetUUID(_Dupe, _UUID)
THEN
NRD_ModCall("EnemyUpgradeOverhaul", "SetChallengePointsTag", _UUID);
*/
//END_REGION

//REGION COPY_STATUSES
QRY
LLENEMY_Duplication_QRY_IgnoreStatus((CHARACTERGUID)_Dupe, (STRING)_Status, (REAL)_Duration)
AND
NOT DB_LLENEMY_Duplication_IgnoredStatus(_Status)
AND
DB_LLENEMY_Upgrades_Statuses("Duplication", _Type, _Status, _Start, _End, _Duration, _CP)
THEN
DB_LLENEMY_Duplication_IgnoredStatus(_Status);

QRY
LLENEMY_Duplication_QRY_IgnoreStatus((CHARACTERGUID)_Dupe, (STRING)_Status, (REAL)_Duration)
AND
NOT DB_LLENEMY_Duplication_IgnoredStatus(_Status)
AND
DB_LLENEMY_IgnoredDuplicantStatuses(_Status)
THEN
DB_LLENEMY_Duplication_IgnoredStatus(_Status);

QRY
LLENEMY_Duplication_QRY_IgnoreStatus((CHARACTERGUID)_Dupe, (STRING)_Status)
AND
NOT DB_LLENEMY_Duplication_IgnoredStatus(_Status)
AND
GetStatusType(_Status, _StatusType)
AND
DB_LLENEMY_IgnoredDuplicantStatusTypes(_StatusType)
THEN
DB_LLENEMY_Duplication_IgnoredStatus(_Status);

QRY
LLENEMY_Duplication_QRY_IgnoreStatus((CHARACTERGUID)_Dupe, (STRING)_Status)
AND
NOT DB_LLENEMY_Duplication_IgnoredStatus(_Status)
AND
DB_LLENEMY_Upgrades_Statuses(_Group, _Type, _Status, _StartInt, _EndInt, _Duration, _ChallengePoints)
AND
NOT LLENEMY_Upgrades_QRY_CanApplyGroupTypeUpgrades(_Dupe, _Group, _Type)
THEN
DB_LLENEMY_Duplication_IgnoredStatus(_Status);

QRY
LLENEMY_Duplication_QRY_IgnoreStatus((CHARACTERGUID)_Dupe, (STRING)_Status)
AND
NOT DB_LLENEMY_Duplication_IgnoredStatus(_Status)
AND
DB_LLENEMY_Upgrades_Statuses(_Group, _Type, _Status, _StartInt, _EndInt, _Duration, _ChallengePoints)
AND
NOT LLENEMY_Upgrades_QRY_CanApplyUpgrade(_Dupe, _Group, _Type, _Status, 6.0)
THEN
DB_LLENEMY_Duplication_IgnoredStatus(_Status);

QRY
LLENEMY_Duplication_QRY_CanCopyStatus((CHARACTERGUID)_Dupe, (STRING)_Status)
AND
NOT DB_LeaderLib_EngineStatus(_Status)
AND
NOT DB_LLENEMY_Duplication_IgnoredStatus(_Status)
AND
NOT LLENEMY_Duplication_QRY_IgnoreStatus(_Dupe, _Status)
THEN
DB_NOOP(1);

/* [OSITOOLS_ONLY]
IF
StoryEvent(_Source, "LLENEMY_Duplication_CopyStatuses")
THEN
NRD_IterateStatuses(_Source, "LLENEMY_Dupe_CopyStatus");

IF
NRD_StatusIteratorEvent("LLENEMY_Dupe_CopyStatus", (CHARACTERGUID)_Source, _Status, _Handle)
AND
DB_LLENEMY_Duplication_Temp_Active(_Source, _Dupe, _Region)
AND
LLENEMY_Duplication_QRY_CanCopyStatus(_Dupe, _Status)
AND
GetUUID(_Source, _SourceUUID)
AND
GetUUID(_Dupe, _DupeUUID)
THEN
NRD_ModCall("EnemyUpgradeOverhaul", "Duplication_CopyStatus", _SourceUUID, _DupeUUID, _Status, (STRING)_Handle);
*/

PROC
LLENEMY_Duplication_CopyStatus((CHARACTERGUID)_DupeSource, (CHARACTERGUID)_Dupe, (STRING)_Status, (REAL)_Duration, (GUIDSTRING)_StatusSource)
AND
_Duration > 0.0
AND
RealSum(_Duration, 0.1, _ExtendedDuration)
THEN
ApplyStatus(_Dupe, _Status, _ExtendedDuration, 1, _StatusSource);

PROC
LLENEMY_Duplication_CopyStatus((CHARACTERGUID)_DupeSource, (CHARACTERGUID)_Dupe, (STRING)_Status, (REAL)_Duration, (GUIDSTRING)_StatusSource)
AND
_Duration < 0.0
THEN
ApplyStatus(_Dupe, _Status, _Duration, 1, _StatusSource);

PROC
LLENEMY_Duplication_CopyStatus((CHARACTERGUID)_DupeSource, (CHARACTERGUID)_Dupe, (STRING)_Status, (REAL)_Duration, (GUIDSTRING)_StatusSource)
AND
DB_LLENEMY_Upgrades_Temp_ChangeDurationOnCombat(_Region, _DupeOwner, _Status, _RealDuration)
THEN
DB_LLENEMY_Upgrades_Temp_ChangeDurationOnCombat(_Region, _Dupe, _Status, _RealDuration);

PROC
LLENEMY_Duplication_CopyStatus((CHARACTERGUID)_DupeSource, (CHARACTERGUID)_Dupe, (STRING)_Status, (REAL)_Duration, (GUIDSTRING)_StatusSource)
AND
DB_LLENEMY_Upgrades_Statuses(_Group, _Type, _Status, _StartInt, _EndInt, _Duration, _ChallengePoints)
THEN
LLENEMY_Upgrades_IncreaseChallengePoints(_Dupe, _ChallengePoints);
//END_REGION

//REGION STAGE_TWO_SETUP
PROC
LLENEMY_Duplication_Internal_SetupDupe_StageTwo((CHARACTERGUID)_Source, (CHARACTERGUID)_Dupe)
AND
CharacterGetLevel(_Source, _Level)
THEN
CharacterLevelUpTo(_Dupe, _Level);

PROC
LLENEMY_Duplication_Internal_SetupDupe_StageTwo((CHARACTERGUID)_Source, (CHARACTERGUID)_Dupe)
THEN
LeaderLib_Tags_PreserveTag(_Dupe, "LLENEMY_Duplicant");
LLENEMY_Duplication_CopyName(_Source, _Dupe);

PROC
LLENEMY_Duplication_Internal_SetupDupe_StageTwo((CHARACTERGUID)_Source, (CHARACTERGUID)_Dupe)
AND
SysStatus("LaughingLeader_FullLoot", _)
THEN
LeaderLib_Tags_PreserveTag(_Dupe, "LLFULOOT_LootDisabled");

/* [OSITOOLS_ONLY]
IF
NRD_SkillIteratorEvent("LLENEMY_Dupe_CopySkill", _Source, _Skill, _IsLearned, _IsActivated)
AND
LLENEMY_Ext_QRY_IgnoreSkill(_Skill, 0)
AND
DB_LLENEMY_Duplication_Temp_Active(_Source, _Dupe, _Region)
THEN
CharacterAddSkill(_Dupe, _Skill, 0);
*/
//END_REGION

//REGION DUPLICANT_NON_EXTENDER
// [BEGIN_NO_OSITOOLS]
PROC
LLENEMY_Duplication_Internal_SetupDupe((CHARACTERGUID)_Source, (CHARACTERGUID)_Dupe, (INTEGER)_CopyStatuses)
THEN
LeaderLib_Tags_ClearPreservedTag(_Dupe, "LLENEMY_UpgradesDisabled");

PROC
LLENEMY_Duplication_Internal_SetupDupe((CHARACTERGUID)_Source, (CHARACTERGUID)_Dupe, (INTEGER)_CopyStatuses)
AND
GetRegion(_Source, _Region)
AND
GetFaction(_Source, _Faction)
THEN
DB_LLENEMY_Duplication_Temp_Active(_Source, _Dupe, _Region);
SetOnStage(_Dupe, 0);
CharacterSetDetached(_Dupe, 1);
SetVarObject(_Dupe, "LLENEMY_Duplicant_Owner", _Source);
ApplyStatus(_Dupe, "LLENEMY_DUPLICANT", -1.0, 0, _Source);
CharacterTransformFromCharacter(_Dupe, _Source, 1, 1, 1, 1, 1, 1, 0);
CharacterTransformAppearanceTo(_Dupe, _Source, 1, 1);
SetFaction(_Dupe, _Faction);
//ChangeAttitude(_Source, _Dupe, 200);
//ChangeAttitude(_Dupe, _Source, 200);
LLENEMY_Duplication_Internal_SetupDupe_StageTwo(_Source, _Dupe);

IF
CharacterCharacterEvent(_Source, _Dupe, "LLENEMY_Duplication_SetupStageTwo")
THEN
LLENEMY_Duplication_Internal_SetupDupe_StageTwo(_Source, _Dupe);

PROC
LLENEMY_Duplication_Internal_SetupDupe_StageTwo((CHARACTERGUID)_Source, (CHARACTERGUID)_Dupe)
AND
DB_LLENEMY_Duplication_Temp_CopySettings(_Source, _Dupe, _CopyAttributes, _CopyAbilities)
THEN
NOT DB_LLENEMY_Duplication_Temp_CopySettings(_Source, _Dupe, _CopyAttributes, _CopyAbilities);
LLENEMY_Duplication_SetupDuplicant(_Source, _Dupe, _CopyAttributes, _CopyAbilities);

PROC
LLENEMY_Duplication_SetupDuplicant((CHARACTERGUID)_Source, (CHARACTERGUID)_Dupe, 1, (INTEGER)_CopyAbilities)
THEN
LeaderLib_Helper_CopyAttributes(_Dupe, _Source);

PROC
LLENEMY_Duplication_SetupDuplicant((CHARACTERGUID)_Source, (CHARACTERGUID)_Dupe, (INTEGER)_CopyAttributes, 1)
THEN
LeaderLib_Helper_CopyAbilities(_Dupe, _Source);

PROC
LLENEMY_Duplication_CopyName((CHARACTERGUID)_Source, (CHARACTERGUID)_Dupe)
AND
CharacterGetDisplayName(_Source, _, _Name)
AND
StringConcatenate(_Name, " (Shadow)", _DupeName)
THEN
CharacterSetCustomName(_Dupe, _DupeName);
// [END_NO_OSITOOLS]
//END_REGION

//REGION COPY_NAME
/* [OSITOOLS_ONLY]
PROC
LLENEMY_Duplication_CopyName((CHARACTERGUID)_Source, (CHARACTERGUID)_Dupe)
AND
GetUUID(_Dupe, _DupeUUID)
AND
GetUUID(_Source, _SourceUUID)
THEN
NRD_ModCall("EnemyUpgradeOverhaul", "Duplication_CopyName", _SourceUUID, _DupeUUID);
*/
//END_REGION

//REGION DUPE_STATUS_APPLIED
IF
CharacterStatusApplied(_Char, "LLENEMY_DUPE_ELITE_SINGLE", _)
AND
DB_LLENEMY_Upgrades_Statuses(_Group, _Type, "LLENEMY_DUPE_ELITE_SINGLE", _StartInt, _EndInt, _Duration, _ChallengePoints)
THEN
LLENEMY_Duplication_CreateDuplicant(_Char, "LLENEMY_Dupe_Elite_A", 1, 1, _ChallengePoints);

IF
CharacterStatusApplied(_Char, "LLENEMY_DUPE_ELITE_DOUBLE", _)
AND
DB_LLENEMY_Upgrades_Statuses(_Group, _Type, "LLENEMY_DUPE_ELITE_DOUBLE", _StartInt, _EndInt, _Duration, _ChallengePoints)
THEN
LLENEMY_Duplication_CreateDuplicant(_Char, "LLENEMY_Dupe_Elite_A", 1, 1, _ChallengePoints);
LLENEMY_Duplication_CreateDuplicant(_Char, "LLENEMY_Dupe_Elite_A", 1, 1, _ChallengePoints);

IF
CharacterStatusApplied(_Char, "LLENEMY_DUPE_NORMAL_SAME", _)
AND
DB_LLENEMY_Upgrades_Statuses(_Group, _Type, "LLENEMY_DUPE_NORMAL_SAME", _StartInt, _EndInt, _Duration, _ChallengePoints)
THEN
LLENEMY_Duplication_CreateDuplicant(_Char, "LLENEMY_Dupe_Normal_A", 1, 1, _ChallengePoints);

IF
CharacterStatusApplied(_Char, "LLENEMY_DUPE_NORMAL_WEAK", _)
AND
DB_LLENEMY_Upgrades_Statuses(_Group, _Type, "LLENEMY_DUPE_NORMAL_WEAK", _StartInt, _EndInt, _Duration, _ChallengePoints)
THEN
LLENEMY_Duplication_CreateDuplicant(_Char, "LLENEMY_Dupe_Normal_Weak", 0, 1, _ChallengePoints);

IF
CharacterStatusApplied(_Char, "LLENEMY_DUPE_NORMAL_WEAKEST", _)
AND
DB_LLENEMY_Upgrades_Statuses(_Group, _Type, "LLENEMY_DUPE_NORMAL_WEAKEST", _StartInt, _EndInt, _Duration, _ChallengePoints)
THEN
LLENEMY_Duplication_CreateDuplicant(_Char, "LLENEMY_Dupe_Normal_Weakest", 0, 1, _ChallengePoints);
//END_REGION

//REGION DUPE_ARENA
PROC
LLENEMY_Duplication_Internal_SetupDupe_StageTwo((CHARACTERGUID)_Source, (CHARACTERGUID)_Dupe)
AND
//DB_ArenaPresets_Mobs(_Arena, _Source, _Trigger)
DB_Arena_MobParticipants(_Inst, _Source, _Trig, _Team)
THEN
DB_Arena_MobParticipants(_Inst, _Dupe, _Trig, _Team);
SetInArena(_Dupe, 1);
SetFaction(_Dupe, _Team);
//END_REGION

//REGION COMBAT_HANDLING
IF
StoryEvent((CHARACTERGUID)_Dupe, "LLENEMY_DupeCharacterTeleported")
AND
DB_LLENEMY_Duplication_Temp_Active(_Source, _Dupe, _Region)
AND
CharacterIsInCombat(_Dupe, 0)
AND
CharacterIsInCombat(_Source, 0)
THEN
SetOnStage(_Dupe, 0);

IF
StoryEvent((CHARACTERGUID)_Dupe, "LLENEMY_DupeCharacterTeleported")
AND
ObjectIsOnStage(_Dupe, 0)
AND
DB_LLENEMY_Duplication_Temp_Active(_Source, _Dupe, _Region)
AND
CharacterIsInCombat(_Source, 1)
AND
CharacterIsDead(_Dupe, 0)
THEN
SetOnStage(_Dupe, 1);
CharacterSetDetached(_Dupe, 0);

IF
ObjectEnteredCombat((CHARACTERGUID)_Source, _CombatID)
AND
DB_LLENEMY_Duplication_Temp_Active(_Source, _Dupe, _Region)
AND
CharacterIsDead(_Dupe, 0)
AND
ObjectIsOnStage(_Dupe, 0)
THEN
TeleportTo(_Dupe, _Source, "LLENEMY_Dupe_OnDupeJoinedCombat", 0, 1);

IF
StoryEvent((CHARACTERGUID)_Dupe, "LLENEMY_Dupe_OnDupeJoinedCombat")
THEN
SetOnStage(_Dupe, 1);
CharacterSetDetached(_Dupe, 0);

IF
ObjectLeftCombat((CHARACTERGUID)_Source, _CombatID)
AND
DB_LLENEMY_Duplication_Temp_Active(_Source, _Dupe, _Region)
AND
CharacterIsDead(_Source, 0)
AND
CharacterIsDead(_Dupe, 0)
AND
ObjectIsOnStage(_Dupe, 1)
THEN
LeaveCombat(_Dupe);
SetOnStage(_Dupe, 0);

IF
CharacterRelationChangedTo(_Source, _Target, _Attitude)
AND
DB_LLENEMY_Duplication_Temp_Active(_Source, _Dupe, _Region)
AND
CharacterIsDead(_Dupe, 0)
THEN
ChangeAttitude(_Dupe, _Target, _Attitude);
//END_REGION

//REGION DEATH_CLEANUP
IF
CharacterDied(_Dupe)
AND
IsTagged(_Dupe, "LLENEMY_Duplicant", 1)
THEN
SetOnStage(_Dupe, 0);

IF
CharacterWentOnStage(_Dupe, 1)
AND
IsTagged(_Dupe, "LLENEMY_Duplicant", 1)
AND
CharacterIsDead(_Dupe, 1)
THEN
SetOnStage(_Dupe, 0);

IF
CharacterDied(_Dupe)
AND
DB_LLENEMY_Duplication_Temp_Active(_Source, _Dupe, _Region)
AND
NOT DB_LLENEMY_Talents_Temp_ReviveNext(_Dupe, _)
THEN
NOT DB_LLENEMY_Duplication_Temp_Active(_Source, _Dupe, _Region);
LeaderLib_Tags_ClearAllPreservedTagData(_Dupe);
SetOnStage(_Dupe, 0);

IF
RegionEnded(_Region)
AND
DB_LLENEMY_Duplication_Temp_Active(_Source, _Dupe, _Region)
THEN
NOT DB_LLENEMY_Duplication_Temp_Active(_Source, _Dupe, _Region);
LeaderLib_Tags_ClearAllPreservedTagData(_Dupe);
SetOnStage(_Dupe, 0);
//END_REGION

/*
IF
CharacterStatusRemoved(_Dupe, "LEADERLIB_RECALC", _)
AND
DB_LLENEMY_Duplication_Temp_Active(_Source, _Dupe, _Region)
THEN
CharacterSetArmorPercentage(_Dupe, 100.0);
CharacterSetMagicArmorPercentage(_Dupe, 100.0);
CharacterSetHitpointsPercentage(_Dupe, 100.0);
*/
EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader_EnemyUpgradeOverhaul"
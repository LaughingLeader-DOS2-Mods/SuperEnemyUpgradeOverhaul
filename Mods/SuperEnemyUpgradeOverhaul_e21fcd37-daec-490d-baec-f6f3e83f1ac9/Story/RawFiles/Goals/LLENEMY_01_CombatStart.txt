Version 1
SubGoalCombiner SGC_AND
INITSECTION

KBSECTION
//REGION SKIP_COMBAT
//Windego tutorial fight. Skip this since it's not actually a real combat, and negative auras may leave permanent debuffs
IF
ObjectEnteredCombat(CHARACTERGUID_S_GLO_Windego_d783285f-d3be-4cba-8333-db8976cef182, _CombatID)
AND
DB_CurrentLevel("TUT_Tutorial_A")
THEN
DB_LLENEMY_SkipCombat(_CombatID);

IF
CombatEnded(_CombatID)
AND
DB_LLENEMY_SkipCombat(_CombatID)
THEN
NOT DB_LLENEMY_SkipCombat(_CombatID);
//END_REGION

//REGION QUEUE_SETUP
IF
DB_LeaderLib_Combat_ActiveCombat(_CombatID)
AND
NOT DB_LLENEMY_SkipCombat(_CombatID)
AND
NOT DB_LLENEMY_CombatQueue(_CombatID, _)
AND
IntegertoString(_CombatID, _CombatIDStr)
AND
StringConcatenate("LLENEMY_CombatQueue_", _CombatIDStr, _TimerName)
THEN
DB_LLENEMY_CombatQueue(_CombatID, _TimerName);
//Reset seed
LeaderLib_Random_CreateSeed();

PROC
LLENEMY_Queue_AddToCombatQueue((CHARACTERGUID)_Enemy, (INTEGER)_CombatID)
AND
DB_LLENEMY_CombatQueue(_CombatID, _TimerName)
AND
NOT DB_Stack_UUID(_Enemy, _)
THEN
TimerCancel(_TimerName);
TimerLaunch(_TimerName, 500);
Stack(_TimerName, _Enemy);

IF
CombatEnded(_CombatID)
AND
DB_LLENEMY_CombatQueue(_CombatID, _TimerName)
THEN
LLENEMY_Queue_ClearData(_CombatID, _TimerName);
NOT DB_LLENEMY_CombatQueue(_CombatID, _TimerName);

PROC
LLENEMY_Queue_ClearData((INTEGER)_CombatID, (STRING)_TimerName)
AND
DB_TopOfStack(_TimerName, _TopValue)
THEN
NOT DB_TopOfStack(_TimerName, _TopValue);

PROC
LLENEMY_Queue_ClearData((INTEGER)_CombatID, (STRING)_TimerName)
AND
DB_StackInternalCounter(_Amount, _TimerName)
THEN
NOT DB_StackInternalCounter(_Amount, _TimerName);

PROC
LLENEMY_Queue_ClearData((INTEGER)_CombatID, (STRING)_TimerName)
AND
DB_StackInternalCounter(_NewAmount, _TimerName)
THEN
NOT DB_StackInternalCounter(_NewAmount, _TimerName);

PROC
LLENEMY_Queue_ClearData((INTEGER)_CombatID, (STRING)_TimerName)
AND
DB_StackInternalMember(_NewAmount, _TimerName, _Value)
THEN
NOT DB_StackInternalMember(_NewAmount, _TimerName, _Value);
//END_REGION

//REGION ROLL_UPGRADES
PROC
LLENEMY_Upgrades_Internal_StartRolling((CHARACTERGUID)_Char, (STRING)_Level)
AND
LLENEMY_Upgrades_QRY_CanAddUpgrades(_Char)
THEN
LLENEMY_Upgrades_RollForUpgrades(_Char);
LLENEMY_Upgrades_OnRollingDone(_Char, _Level);

IF
ObjectTransformed((CHARACTERGUID)_Char, _)
THEN
ObjectClearFlag(_Char, "LLENEMY_WorldUpgradesAdded", 0);
LLENEMY_Upgrades_UpgradeEnemy(_Char);
//END_REGION

//REGION ADD_TO_QUEUE
IF
ObjectEnteredCombat((CHARACTERGUID)_Char, _CombatID)
AND
NOT DB_LLENEMY_SkipCombat(_CombatID)
AND
LLENEMY_Upgrades_QRY_CanAddUpgrades(_Char)
AND
NOT LeaderLib_Helper_QRY_IgnoreCharacter(_Char)
AND
NOT LeaderLib_Helper_QRY_CharacterIsPlayer(_Char)
THEN
LLENEMY_OnCharacterJoinedCombat(_Char, _CombatID);

//NPC became hostile in the middle of combat
PROC
ProcMakeNPCHostile((CHARACTERGUID)_Player, (CHARACTERGUID)_Enemy)
AND
NOT LeaderLib_Helper_QRY_IgnoreCharacter(_Enemy)
AND
NOT LeaderLib_Helper_QRY_CharacterIsPlayer(_Enemy)
AND
LLENEMY_Upgrades_QRY_CanAddUpgrades(_Enemy)
AND
CombatGetIDForCharacter(_Enemy, _CombatID)
AND
NOT DB_LLENEMY_SkipCombat(_CombatID)
THEN
LLENEMY_OnCharacterJoinedCombat(_Enemy, _CombatID);

PROC
LLENEMY_OnCharacterJoinedCombat((CHARACTERGUID)_Char, (INTEGER)_CombatID)
AND
NOT DB_LLENEMY_IgnoredCharacters(_Char)
AND
LLENEMY_QRY_IsEnemyOfParty(_Char, _CombatID)
AND
DB_LLENEMY_CombatQueue(_CombatID, _TimerName)
THEN
LLENEMY_Queue_AddToCombatQueue(_Char, _CombatID);
//END_REGION

//REGION QUEUE_RUNNING
IF
TimerFinished(_TimerName)
AND
DB_LLENEMY_CombatQueue(_CombatID, _TimerName)
AND
NOT DB_LLENEMY_SkipCombat(_CombatID)
AND
DB_TopOfStackObject(_TimerName, _Enemy)
AND
DB_Stack_UUID(_Enemy, _UUID)
AND
ObjectIsCharacter((CHARACTERGUID)_Enemy, 1)
AND
GetRegion(_Enemy, _Region)
THEN
NOT DB_Stack_UUID(_Enemy, _UUID);
LeaderLog_Log("DEBUG", "[LLENEMY:CombatQueue:DB_TopOfStack] Applying buffs to character in via combat queue [",_TimerName,"].");
LLENEMY_Upgrades_Internal_StartRolling(_Enemy, _Region);
PopStack(_TimerName);

IF
TimerFinished(_TimerName)
AND
DB_LLENEMY_CombatQueue(_CombatID, _TimerName)
AND
NOT DB_LLENEMY_SkipCombat(_CombatID)
AND
DB_StackInternalCounter(_Amount, _TimerName)
AND
_Amount > 0
THEN
TimerCancel(_TimerName);
TimerLaunch(_TimerName, 150);

IF
TimerFinished(_TimerName)
AND
DB_LLENEMY_CombatQueue(_CombatID, _TimerName)
AND
NOT DB_LLENEMY_SkipCombat(_CombatID)
AND
DB_StackInternalCounter(_Amount, _TimerName)
AND
_Amount <= 0
THEN
LeaderLog_Log("DEBUG", "[LLENEMY:CombatQueue:TimerFinished] Combat Queue [",_TimerName,"] was completed.");

IF
TimerFinished(_TimerName)
AND
DB_LLENEMY_CombatQueue(_CombatID, _TimerName)
AND
DB_LLENEMY_SkipCombat(_CombatID)
THEN
LLENEMY_Queue_ClearData(_CombatID, _TimerName);
LeaderLog_LogInt("DEBUG", "[LLENEMY:CombatQueue:TimerFinished] Skipping combat with ID [",_CombatID,"].");
//END_REGION

//REGION POST_QUEUE
PROC
LLENEMY_OnCharacterJoinedCombat((CHARACTERGUID)_Enemy, (INTEGER)_CombatID)
AND
LLENEMY_QRY_IsEnemyOfParty(_Enemy, _CombatID)
AND
NOT DB_LLENEMY_CombatQueue(_CombatID, _)
AND
GetRegion(_Enemy, _Region)
THEN
LeaderLog_Log("DEBUG", "[LLENEMY:CombatQueue:OnCharacterJoinedCombat] Combat queue doesn't exist. Applying buffs instantly.");
LLENEMY_Upgrades_Internal_StartRolling(_Enemy, _Region);

// Player joined ongoing NPC vs. NPC combat
IF
ObjectEnteredCombat((CHARACTERGUID)_Player, _CombatID)
AND
CharacterIsPlayer(_Player, 1)
AND
NOT DB_LLENEMY_SkipCombat(_CombatID)
AND
DB_CombatCharacters(_Char, _CombatID)
AND
LLENEMY_Upgrades_QRY_CanAddUpgrades(_Char)
AND
NOT LeaderLib_Helper_QRY_IgnoreCharacter(_Char)
AND
NOT LeaderLib_Helper_QRY_CharacterIsPlayer(_Char)
THEN
LLENEMY_OnCharacterJoinedCombat(_Char, _CombatID);
//END_REGION

EXITSECTION

ENDEXITSECTION
ParentTargetEdge "LaughingLeader_EnemyUpgradeOverhaul"
